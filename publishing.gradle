/*
 * Copyright (C) 2019 POP Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Publish the source code with the library
 */
task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.java.source
}

/**
 * Publish javadoc with library
 */
task javadocsJar(type: Jar, dependsOn: dokka) {
  classifier = 'javadoc'
  from "$buildDir/dokka"
}

artifacts {
  archives sourcesJar
  archives javadocsJar
}

project.afterEvaluate {
  publishing {
    publications {
      AutoAnalytics(MavenPublication) {
        artifact jar
        artifact sourcesJar
        artifact javadocsJar
        groupId POM_GROUP
        artifactId project.name
        version POM_VERSION

        pom.withXml {
          def dependenciesNode = asNode().appendNode('dependencies')

          configurations.implementation.allDependencies.each {
            // Ensure dependencies such as fileTree are not included in the pom.
            if (it.name != 'unspecified') {
              def dependencyNode = dependenciesNode.appendNode('dependency')
              dependencyNode.appendNode('groupId', it.group)
              dependencyNode.appendNode('artifactId', it.name)
              dependencyNode.appendNode('version', it.version)
            }
          }
        }
      }
    }
  }
}

final String GITHUB_ROOT_URL = "https://github.com/POPinNow/AutoAnalytics"
bintray {
  user = 'popindeveloper'
  key = BINTRAY_API_KEY
  publications = ['AutoAnalytics']
  override = true
  pkg {
    repo = 'AutoAnalytics'
    name = project.name
    desc = "Automatically generate analytics tracking via annotation processing"
    licenses = ['Apache-2.0']
    vcsUrl = "${GITHUB_ROOT_URL}"
    websiteUrl = "${GITHUB_ROOT_URL}"
    version {
      name = POM_VERSION
      issueTrackerUrl = "${GITHUB_ROOT_URL}/issues"
      desc = "Automatically generate analytics tracking via annotation processing"
      released = new Date()
      vcsTag = POM_VERSION
    }
  }
}

